load("@bazel_skylib//rules:native_binary.bzl", "native_test")
load("@rules_ll//ll:defs.bzl", "ll_compilation_database")

filegroup(
    name = "clang_tidy_config",
    srcs = [".clang-tidy"],
    visibility = ["//:__subpackages__"],
)

ll_compilation_database(
    name = "compile_commands",
    config = ":clang_tidy_config",
    targets = [
        # "//clang_tidy_example",  # This one runs separately.
        "//cuda_example",
        "//format_example",
        "//frontend_action_example",
        "//external_dependency_example",
        "//hip_example:amdgpu",
        "//hip_rdc_example:amdgpu",
        # "//module_partition_example",  # Bugged. Need to report upstream.
        "//modules_example",
        # "//modules_draft_example",  # Not clang-tidy conform.
        "//openmp_example",
        # "//sanitizers",  # Not clang-tidy conform.
        "//shared_library_example",
    ],
)

# Tests start below.

test_suite(
    name = "all",
    tests = [
        ":amdgpu",
        ":cpp",
        ":nvptx",
    ],
)

# CPU tests.

EXAMPLES = [
    "format_example",
    "frontend_action_example",
    "external_dependency_example",
    "modules_example",
    "module_partition_example",
    "openmp_example",
    # "coverage_example",
    # "sanitizers",
    # "clang_tidy_example",
    "shared_library_example",
    "std_module_example",
    "modules_draft_example",
]

test_suite(
    name = "cpp",
    tests = [
        "{}_test".format(name)
        for name in EXAMPLES
    ],
)

[
    native_test(
        name = "{}_test".format(name),
        timeout = "short",
        src = "//{}".format(name),
        out = "{}_test".format(name),
    )
    for name in EXAMPLES
]

# NVPTX tests.

test_suite(
    name = "nvptx",
    tags = ["nvptx"],
    tests = [
        "//cuda_example:test",
        "//hip_example:nvptx_test",
        "//hip_modules_example:nvptx_test",
        "//hip_rdc_example:nvptx_test",
    ],
)

# AMDGPU tests.

test_suite(
    name = "amdgpu",
    tags = ["amdgpu"],
    tests = [
        "//hip_example:amdgpu_test",
        "//hip_modules_example:amdgpu_test",
        "//hip_rdc_example:amdgpu_test",
    ],
)
