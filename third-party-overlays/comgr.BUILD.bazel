"Build file for ROCm-CompilerSupport."

load("@rules_ll//ll:defs.bzl", "ll_library")
load("@rules_ll//third-party-overlays:defs.bzl", "opencl_pch")
load("@bazel_skylib//rules:expand_template.bzl", "expand_template")
load("@bazel_skylib//rules:common_settings.bzl", "bool_flag")

bool_flag(
    name = "shared_amd_comgr",
    build_setting_default = False,
)

config_setting(
    name = "shared",
    flag_values = {":shared_amd_comgr": "true"},
)

expand_template(
    name = "amd_comgr_h_gen",
    out = "comgr/amd_comgr/amd_comgr.h",
    substitutions = {
        "@amd_comgr_VERSION_MAJOR@": "2",
        "@amd_comgr_VERSION_MINOR@": "5",
        "@amd_comgr_NAME@": "amd_comgr",
    },
    template = "lib/comgr/include/amd_comgr.h.in",
)

# Can't use ll_binary for now, as our transitions break.
cc_binary(
    name = "bc2h",
    srcs = ["lib/comgr/bc2h.c"],
)

AMD_DEVICE_LIBS_TARGETS = [
    "oclc_abi_version_400",
    "oclc_abi_version_500",
    "oclc_correctly_rounded_sqrt_off",
    "oclc_correctly_rounded_sqrt_on",
    "oclc_daz_opt_off",
    "oclc_daz_opt_on",
    "oclc_finite_only_off",
    "oclc_finite_only_on",
    "oclc_isa_version_1010",
    "oclc_isa_version_1011",
    "oclc_isa_version_1012",
    "oclc_isa_version_1013",
    "oclc_isa_version_1030",
    "oclc_isa_version_1031",
    "oclc_isa_version_1032",
    "oclc_isa_version_1033",
    "oclc_isa_version_1034",
    "oclc_isa_version_1035",
    "oclc_isa_version_1036",
    "oclc_isa_version_1100",
    "oclc_isa_version_1101",
    "oclc_isa_version_1102",
    "oclc_isa_version_1103",
    "oclc_isa_version_600",
    "oclc_isa_version_601",
    "oclc_isa_version_602",
    "oclc_isa_version_700",
    "oclc_isa_version_701",
    "oclc_isa_version_702",
    "oclc_isa_version_703",
    "oclc_isa_version_704",
    "oclc_isa_version_705",
    "oclc_isa_version_801",
    "oclc_isa_version_802",
    "oclc_isa_version_803",
    "oclc_isa_version_805",
    "oclc_isa_version_810",
    "oclc_isa_version_900",
    "oclc_isa_version_902",
    "oclc_isa_version_904",
    "oclc_isa_version_906",
    "oclc_isa_version_908",
    "oclc_isa_version_909",
    "oclc_isa_version_90a",
    "oclc_isa_version_90c",
    "oclc_isa_version_940",
    "oclc_unsafe_math_off",
    "oclc_unsafe_math_on",
    "oclc_wavefrontsize64_off",
    "oclc_wavefrontsize64_on",
    "ocml",
    "ockl",
    "opencl",
    "hip",
    "asanrtl",
]

# TODO: It is unfortunate that we can't really build these in parallel with the
# current build files. The rocm-device-libs block this target.
genrule(
    name = "device_lib_headers_gen",
    srcs = ["@rocm-device-libs//:rocm-device-libs"],
    tools = [":bc2h"],
    cmd = """
for f in $(SRCS)
do
    base=$$(basename $$f)
    without_extension=$${base%.*}
    $(location :bc2h) $$f $(RULEDIR)/comgr/internal/$${without_extension}.inc "$${without_extension}_lib"
done
    """,
    outs = [
        "comgr/internal/{}.inc".format(name)
        for name in AMD_DEVICE_LIBS_TARGETS
    ],
)

genrule(
    name = "libraries_inc_gen",
    srcs = [":device_lib_headers_gen"],
    cmd = """
for f in $(SRCS)
do
    echo '#include "'"$$(basename $$f)"'"'  >> $(OUTS)
done
    """,
    outs = ["comgr/internal/libraries.inc"],
)

genrule(
    name = "libraries_defs_inc_gen",
    srcs = [":device_lib_headers_gen"],
    cmd = """
cat <<EOF >> $(OUTS)
#ifndef AMD_DEVICE_LIBS_TARGET
#define AMD_DEVICE_LIBS_TARGET(t)
#endif
#ifndef AMD_DEVICE_LIBS_GFXIP
#define AMD_DEVICE_LIBS_GFXIP(t, g)
#endif
#ifndef AMD_DEVICE_LIBS_FUNCTION
#define AMD_DEVICE_LIBS_FUNCTION(t, f)
#endif

EOF

for f in $(SRCS)
do
    base=$$(basename $$f)
    without_extension=$${base%.*}
    echo "AMD_DEVICE_LIBS_TARGET("$$without_extension")" >> $(OUTS)

    if [[ $$without_extension == oclc_isa_version_* ]]
    then
        isa_version=$${without_extension##oclc_isa_version_}
        echo 'AMD_DEVICE_LIBS_GFXIP('$$without_extension', "'$$isa_version'")' >> $(OUTS)
    fi

    if [[ $$without_extension == oclc_*_on ]]
    then
        cut_oclc_prefix=$${without_extension##oclc_}
        cut_on_suffix=$${cut_oclc_prefix%_on}
        echo "AMD_DEVICE_LIBS_FUNCTION("$$without_extension", "$$cut_on_suffix")" >> $(OUTS)
    fi

done

cat <<EOF >> $(OUTS)

#undef AMD_DEVICE_LIBS_TARGET
#undef AMD_DEVICE_LIBS_GFXIP
#undef AMD_DEVICE_LIBS_FUNCTION
EOF
    """,
    outs = ["comgr/internal/libraries_defs.inc"],
)

opencl_pch(
    name = "opencl_1_2_pch_gen",
    cl_std = "1.2",
)

genrule(
    name = "opencl_1_2_inc_gen",
    tools = [":bc2h"],
    srcs = [":opencl_1_2_pch_gen"],
    cmd = "$(location :bc2h) $(SRCS) $(OUTS) opencl1_2_c",
    outs = ["comgr/internal/opencl1.2-c.inc"],
)

opencl_pch(
    name = "opencl_2_0_pch_gen",
    cl_std = "2.0",
)

genrule(
    name = "opencl_2_0_inc_gen",
    tools = [":bc2h"],
    srcs = [":opencl_2_0_pch_gen"],
    cmd = "$(location :bc2h) $(SRCS) $(OUTS) opencl2_0_c",
    outs = ["comgr/internal/opencl2.0-c.inc"],
)

ll_library(
    name = "amd_comgr",
    # The only externally visible entrypoint to this target is the generated
    # amd_comgr.h header.
    exposed_includes = ["$(GENERATED)/comgr"],
    exposed_hdrs = [":amd_comgr_h_gen"],
    includes = [
        "lib/comgr/src",
        "$(GENERATED)/comgr/internal",
        "$(GENERATED)/comgr/amd_comgr",
    ],
    compile_flags = [
        "-std=c++17",
        "-O3",  # Usually inherits O2 from the llvm build. Let's just use O3.
        "-fno-rtti",
        "-Wall",
        "-Wno-attributes",
        "-fms-extensions",
        "-fvisibility=hidden",
        "-fno-strict-aliasing",
    ],
    hdrs = glob([
        "lib/comgr/src/*.h",
        "lib/comgr/src/time-stat/*.h",
        "lib/comgr/src/comgr-isa-metadata.def",
    ]) + [
        ":libraries_defs_inc_gen",
        ":libraries_inc_gen",
        ":opencl_1_2_inc_gen",
        ":opencl_2_0_inc_gen",
        ":device_lib_headers_gen",
    ],
    srcs = [
        "lib/comgr/src/comgr-compiler.cpp",
        "lib/comgr/src/comgr.cpp",
        "lib/comgr/src/comgr-device-libs.cpp",
        "lib/comgr/src/comgr-disassembly.cpp",
        "lib/comgr/src/comgr-elfdump.cpp",
        "lib/comgr/src/comgr-env.cpp",
        "lib/comgr/src/comgr-metadata.cpp",
        "lib/comgr/src/comgr-objdump.cpp",
        "lib/comgr/src/comgr-signal.cpp",
        "lib/comgr/src/comgr-symbol.cpp",
        "lib/comgr/src/comgr-symbolizer.cpp",
        "lib/comgr/src/time-stat/time-stat.cpp",
    ],
    depends_on_llvm = 1,
    defines = ["AMD_COMGR_BUILD"],
    visibility = ["//visibility:public"],
    emit = select({
        ":shared": ["shared_object"],
        "//conditions:default": ["archive"],
    }),
    shared_object_link_flags = select({
        ":shared": ["--no-undefined"],
        "//conditions:default": [],
    }),
)
