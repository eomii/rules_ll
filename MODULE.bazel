module(
    name = "rules_ll",
    version = "20230411.0",
    compatibility_level = 0,
)

register_execution_platforms("@rules_ll//rbe/default/config:platform")
register_toolchains(
    "@rules_ll//ll:ll_toolchain",
    "@rules_ll//rbe/default/config:cc-toolchain",
    "@rules_ll//rbe/default/java:all",
)

# Platform support.
bazel_dep(name = "platforms", version = "0.0.8")
bazel_dep(name = "rules_cc", version = "0.0.6")

# Various utility functions such as path manipulations and templating.
bazel_dep(name = "bazel_skylib", version = "1.4.2")

# Documentation. These should be dev_dependencies, but that doesn't work at the
# moment. This is a bug.
bazel_dep(name = "rules_java", version = "6.2.2", dev_dependency = False)
bazel_dep(name = "stardoc", version = "0.5.6", dev_dependency = False)

# The LLVM project. We override the specific commit below.
bazel_dep(name = "llvm-project-overlay", version = "17-init-bcr.3")

# Configure the llvm-project Bazel overlay.
llvm_project_overlay = use_extension(
    "@llvm-project-overlay//utils/bazel:extensions.bzl",
    "llvm_project_overlay",
)
llvm_project_overlay.configure(
    commit = "582e1d58bd43af138033b98b736d97cc89c7b228",
    sha256 = "1b9f2f4ca426716a54fec16835cdf756ed8040fe98c7d67e047f8305ab7aae08",
    targets = ["AMDGPU", "NVPTX", "WebAssembly", "X86"],
    patches = [
        "@rules_ll//patches:mallinfo2_patch.diff",
        "@rules_ll//patches:rules_ll_overlay_patch.diff",
    ],
)

use_repo(
    llvm_project_overlay,
    "llvm-project",
    "llvm-raw",
)

# Set up dependencies for rules_ll.
rules_ll_dependencies = use_extension(
    "@rules_ll//ll:init.bzl",
    "rules_ll_dependencies",
)

use_repo(
    rules_ll_dependencies,
    "zstd",
    "zlib-ng",
    "comgr",
    "hip",
    "hipamd",
    "rocclr",
    "rocm-device-libs",
    "rocm-opencl-runtime",
    "rocr",
    "roct",
)

bazel_dep(name = "rules_go", version = "0.41.0")

# This overrides the Go dependency github.com/cloudflare/circl, which requires
# custom patches beyond what Gazelle can generate.
bazel_dep(name = "circl", version = "1.3.3")

bazel_dep(name = "gazelle", version = "0.32.0")

go_deps = use_extension("@gazelle//:extensions.bzl", "go_deps")
go_deps.from_file(go_mod = "//:go.mod")
use_repo(
    go_deps,
    "com_github_go_git_go_git_v5",
    "com_github_pulumi_pulumi_kubernetes_sdk_v4",
    "com_github_pulumi_pulumi_sdk_v3",
    "io_k8s_sigs_kind",
)
