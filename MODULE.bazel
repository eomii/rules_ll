module(
    name = "rules_ll",
    version = "20230411.0",
    compatibility_level = 0,
)

register_execution_platforms("@rules_ll//rbe/default/config:platform")
register_toolchains(
    "@rules_ll//ll:ll_toolchain",
    "@rules_ll//rbe/default/config:cc-toolchain",
    "@rules_ll//rbe/default/java:all",
)

# Platform support.
bazel_dep(name = "platforms", version = "0.0.6")
bazel_dep(name = "rules_cc", version = "0.0.6")

# Various utility functions such as path manipulations and templating.
bazel_dep(name = "bazel_skylib", version = "1.4.1")

# Documentation.
bazel_dep(name = "stardoc", version = "0.5.3")

# The LLVM project. We override the specific commit below.
bazel_dep(name = "llvm-project-overlay", version = "17-init-bcr.3")

# Configure the llvm-project Bazel overlay.
llvm_project_overlay = use_extension(
    "@llvm-project-overlay//utils/bazel:extensions.bzl",
    "llvm_project_overlay",
)
llvm_project_overlay.configure(
    commit = "762c709911d3f4a9086239f226a6439a6bc8911c",
    sha256 = "cac09bb765371ebea6b41bb5e6c57f5dc9ff905d53e077dbec5851e39d36a273",
    targets = ["AMDGPU", "NVPTX", "X86", "WebAssembly"],
    patches = [
        "@rules_ll//patches:back_inserter_patch.diff",
        "@rules_ll//patches:compiler-rt_float128_patch.diff",
        "@rules_ll//patches:mallinfo2_patch.diff",
        "@rules_ll//patches:rules_ll_overlay_patch.diff",
        "@rules_ll//patches:clang_new_offload_driver.diff",
        "@rules_ll//patches:llvm_use_zlib-ng.diff",
        "@rules_ll//patches:libcxx_combined_init_patch.diff",
    ],
)

use_repo(
    llvm_project_overlay,
    "llvm-project",
    "llvm-raw",
)

# Set up dependencies for rules_ll.
rules_ll_dependencies = use_extension(
    "@rules_ll//ll:init.bzl",
    "rules_ll_dependencies",
)

use_repo(
    rules_ll_dependencies,
    "zstd",
    "zlib-ng",
    "comgr",
    "hip",
    "hipamd",
    "opensycl",
    "rocclr",
    "rocm-device-libs",
    "rocm-opencl-runtime",
    "rocr",
    "roct",
)

bazel_dep(name = "rules_go", version = "0.39.1")
bazel_dep(name = "gazelle", version = "23226de")
go_deps = use_extension("@gazelle//:extensions.bzl", "go_deps")
go_deps.from_file(go_mod = "//:go.mod")
go_deps.module_override(
    path = "github.com/cloudflare/circl",
    patches = [
        "@rules_ll//patches:go_circl_header_fix.diff",
    ],
    patch_strip = 1,
)
use_repo(
    go_deps,
    "com_github_pulumi_pulumi_kubernetes_sdk_v3",
    "com_github_pulumi_pulumi_sdk_v3",
    "io_k8s_sigs_kind",
)
