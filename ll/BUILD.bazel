load("@bazel_skylib//:bzl_library.bzl", "bzl_library")
load("@bazel_skylib//rules:common_settings.bzl", "string_flag")
load("@stardoc//stardoc:stardoc.bzl", "stardoc")
load("//ll:toolchain.bzl", "ll_toolchain")
load("//ll:transitions.bzl", "COMPILATION_MODES")

LL_MODULES = [
    "actions",
    "args",
    "attributes",
    "compilation_database",
    "coverage",
    "defs",
    "driver",
    "environment",
    # "init",  # Broken until stardoc supports module_extension and tag_class.
    "inputs",
    "ll",
    "llvm_project_deps",
    "outputs",
    "providers",
    "resolve_rule_inputs",
    "toolchain",
    "tools",
    "transitions",
]

bzl_library(
    name = "ll_bzl",
    srcs = [
        "{}.bzl".format(name)
        for name in LL_MODULES
    ],
    deps = [
        "@bazel_skylib//lib:dicts",
        "@bazel_skylib//lib:paths",
        "@bazel_skylib//rules:common_settings",
        "@bazel_tools//tools/build_defs/repo:http.bzl",
        "@bazel_tools//tools/build_defs/repo:utils.bzl",
    ],
)

[
    stardoc(
        name = "{}_docs".format(name),
        out = "{}.md".format(name),
        func_template = "templates/function_template.vm",
        header_template = "templates/header_template.vm",
        input = "{}.bzl".format(name),
        provider_template = "templates/provider_template.vm",
        rule_template = "templates/rule_template.vm",
        deps = [
            ":ll_bzl",
        ],
    )
    for name in LL_MODULES
]

filegroup(
    name = "docs",
    srcs = [
        ":{}_docs".format(name)
        for name in LL_MODULES
    ],
)

# Constraints.

constraint_setting(name = "ll_exec")

constraint_value(
    name = "ll_exec_linux",
    constraint_setting = ":ll_exec",
)

# Platforms.

platform(
    name = "ll_linux_exec_platform",
    constraint_values = [
        "@platforms//os:linux",
        "@platforms//cpu:x86_64",
        ":ll_exec_linux",
    ],
)

# Configurations.

string_flag(
    name = "current_ll_toolchain_configuration",
    build_setting_default = "cpp",
    values = COMPILATION_MODES,
    visibility = ["//visibility:public"],
)

[
    config_setting(
        name = mode,
        flag_values = {
            ":current_ll_toolchain_configuration": mode,
        },
    )
    for mode in COMPILATION_MODES
]

# Toolchain Types.

toolchain_type(name = "toolchain_type")

# Toolchains.

ll_toolchain(
    name = "ll_toolchain_x86_64-unknown-linux-gnu",
    address_sanitizer = select({
        ":bootstrap": [],
        "//conditions:default": [
            "@llvm-project//compiler-rt/lib/asan:clang_rt.asan",
            "@llvm-project//compiler-rt/lib/asan:clang_rt.asan_static",
            "@llvm-project//compiler-rt/lib/asan:clang_rt.asan_cxx",
        ],
    }),
    compiler_runtime = select({
        ":bootstrap": [],
        "//conditions:default": [
            "@llvm-project//compiler-rt:libll_compiler-rt",
            "@llvm-project//compiler-rt/lib/crt:crt",
        ],
    }),
    cpp_abihdrs = select({
        ":bootstrap": None,
        "//conditions:default": "@llvm-project//libcxxabi:libcxxabi_headers",
    }),
    cpp_abilib = select({
        ":bootstrap": None,
        "//conditions:default": "@llvm-project//libcxxabi:libll_cxxabi",
    }),
    cpp_stdhdrs = select({
        ":bootstrap": None,
        "//conditions:default": "@llvm-project//libcxx:libcxx_headers",
    }),
    cpp_stdlib = select({
        ":bootstrap": None,
        "//conditions:default": "@llvm-project//libcxx:libll_cxx",
    }),
    cuda_toolkit = select({
        ":bootstrap": [],
        ":cpp": [],
        ":omp_cpu": [],
        "//conditions:default": [
            "@cuda_cudart//:contents",
            "@cuda_cupti//:contents",
            "@cuda_nvcc//:contents",
            "@cuda_nvprof//:contents",
            "@cuda_profiler_api//:contents",
            "@libcurand//:contents",
        ],
    }),
    exec_compatible_with = [":ll_exec_linux"],
    hip_libraries = select({
        ":hip_nvptx": [
            "@hip//:headers",
            "@hipamd//:headers",
        ],
        "//conditions:default": [],
    }),
    hipsycl_cuda_backend = select({
        ":sycl_cuda": "@hipsycl//:rt-backend-cuda",
        "//conditions:default": None,
    }),
    hipsycl_hdrs = select({
        ":sycl_cpu": ["@hipsycl//:sycl_headers"],
        ":sycl_cuda": ["@hipsycl//:sycl_headers"],
        "//conditions:default": [],
    }),
    hipsycl_omp_backend = select({
        ":sycl_cpu": "@hipsycl//:rt-backend-omp",
        ":sycl_cuda": "@hipsycl//:rt-backend-omp",
        "//conditions:default": None,
    }),
    hipsycl_plugin = select({
        ":sycl_cpu": "@hipsycl//:libhipSYCL_clang",
        ":sycl_cuda": "@hipsycl//:libhipSYCL_clang",
        "//conditions:default": None,
    }),
    hipsycl_runtime = select({
        ":sycl_cpu": "@hipsycl//:hipSYCL-rt",
        ":sycl_cuda": "@hipsycl//:hipSYCL-rt",
        "//conditions:default": None,
    }),
    leak_sanitizer = select({
        ":bootstrap": [],
        "//conditions:default": [
            "@llvm-project//compiler-rt/lib/lsan:clang_rt.lsan",
        ],
    }),
    libomp = select({
        "omp_cpu": "@llvm-project//openmp:libomp",
        "//conditions:default": None,
    }),
    memory_sanitizer = select({
        ":bootstrap": [],
        "//conditions:default": [
            "@llvm-project//compiler-rt/lib/msan:clang_rt.msan",
            "@llvm-project//compiler-rt/lib/msan:clang_rt.msan_cxx",
        ],
    }),
    omp_header = select({
        "omp_cpu": "@llvm-project//openmp:omp_header",
        "//conditions:default": None,
    }),
    profile = select({
        ":bootstrap": None,
        "//conditions:default": "@llvm-project//compiler-rt/lib/profile:clang_rt.profile",
    }),
    thread_sanitizer = select({
        ":bootstrap": None,
        "//conditions:default": [
            "@llvm-project//compiler-rt/lib/tsan:clang_rt.tsan",
            "@llvm-project//compiler-rt/lib/tsan:clang_rt.tsan_cxx",
        ],
    }),
    undefined_behavior_sanitizer = select({
        ":bootstrap": [],
        "//conditions:default": [
            "@llvm-project//compiler-rt/lib/ubsan:clang_rt.ubsan_standalone",
            "@llvm-project//compiler-rt/lib/ubsan:clang_rt.ubsan_standalone_cxx",
        ],
    }),
    unwind_library = select({
        ":bootstrap": None,
        "//conditions:default": "@llvm-project//libunwind:libll_unwind",
    }),
    visibility = ["//visibility:public"],
)

toolchain(
    name = "ll_toolchain",
    exec_compatible_with = [":ll_exec_linux"],
    toolchain = ":ll_toolchain_x86_64-unknown-linux-gnu",
    toolchain_type = ":toolchain_type",
    visibility = ["//visibility:public"],
)
